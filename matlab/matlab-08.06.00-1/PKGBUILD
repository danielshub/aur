# Maintainer: Daniel E. Shub <daniel.e.shub@gmail.com>

## This PKGBUILD creates an Arch Linux package for the proprietary MATLAB application. A license from The MathWorks is needed in order to both build the package and to run MATLAB once the package is installed. In order to build the package the user must supply a plain text file installation key and the software. For network installations, in addition to the file installation key, a license file needs to be used for the installation. The ISO file can be downloaded from The MathWorks, generated from the official DVD, or created by using the interactive installer to download the toolboxes (installation can be made to a temporary directory and canceled once the toolboxes are downloaded). The contents of the ISO file must include: ./archives/ ./bin/ ./etc/ ./help/ ./java/ /sys ./activate.ini ./install ./installer_input.txt

pkgname=matlab
pkgver=08.06.00
pkgrel=1
pkgdesc='A high-level language for numerical computation and visualization'
arch=('x86_64')
url='http://www.mathworks.com'
license=(custom)
makedepends=('gendesk')
depends=('glu'
         'libxmu'
         'libxp'
         'libxpm'
         'libxrandr'
         'libxtst'
         'ncurses5-compat-libs')
optdepends=('alsa-lib: For sound'
            'gcc47: For MEX support'
            'gcc47-fortran: For MEX support')
source=("${pkgname}.iso"
        "${pkgname}.fik")
md5sums=('SKIP'
         'SKIP')

prepare() {
   msg2 'Creating desktop file'
   gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --categories "Development;Education;Science;Mathematics;IDE" > /dev/null
   sed -i "/^Exec=/ s,$, -desktop," "${srcdir}/${pkgname}.desktop"

   msg2 'Extracting file installation key'
   _fik=$(grep -o [0-9-]* ${pkgname}.fik)

   msg2 'Modifying the installer settings'
   sed -i "s,^# destinationFolder=,destinationFolder=${pkgdir}/opt/tmw/${pkgname}/," "${srcdir}/installer_input.txt"
   sed -i "s,^# agreeToLicense=,agreeToLicense=yes," "${srcdir}/installer_input.txt"
   sed -i "s,^# mode=,mode=silent," "${srcdir}/installer_input.txt"
   sed -i "s,^# fileInstallationKey=,fileInstallationKey=${_fik}," "${srcdir}/installer_input.txt"
  if [ "${_networkinstall}" = true ]; then
    sed -i "s,^# licensePath=,licensePath=${srcdir}/${pkgname}.lic," "${srcdir}/installer_input.txt"
  fi
  if [ "${_partialinstall}" = true ]; then
    msg2 'Building a package with a subset of the licensed products.'
    for _product in "${_products[@]}"; do
      sed -i "/^#product.${_product}$/ s/^#//" "${srcdir}/installer_input.txt"
    done
  fi
}

package() {
  msg2 'Starting MATLAB installer'
  "${srcdir}/install" -t -inputFile "${srcdir}/installer_input.txt" -mode silent &> /dev/null

  msg2 'Installing license'
  install -D -m644 "${pkgdir}/opt/tmw/${pkgname}/license_agreement.txt" "${pkgdir}/usr/share/licenses/tmw/${pkgname}/LICENSE"

  msg2 'Creating links for license.lic'
  ln -s "/etc/tmw/${pkgname}/licenses/" "${pkgdir}/opt/tmw/${pkgname}/licenses"

  msg2 'Creating links for executables'
  install -d -m755 "${pkgdir}/usr/bin/"
  for _executable in deploytool matlab mbuild mcc mex; do
    ln -s "/opt/tmw/${pkgname}/bin/${_executable}" "${pkgdir}/usr/bin/${_executable}"
  done

  msg2 'Installing desktop files'
  install -D -m644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${pkgdir}/opt/tmw/${pkgname}/help/matlab/matlab_env/matlab_desktop_icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  msg2 'Configuring mex options'
  sed -i "s#CC='gcc'#CC='gcc-4.7'#g" "${pkgdir}/opt/tmw/${pkgname}/bin/mexopts.sh"
  sed -i "s#CXX='g++'#CXX='g++-4.7'#g" "${pkgdir}/opt/tmw/${pkgname}/bin/mexopts.sh"
  sed -i "s#FC='gfortran'#FC='gfortran-4.7'#g" "${pkgdir}/opt/tmw/${pkgname}/bin/mexopts.sh"

  ## See $MATLABROOT/sys/os/glnxa64/README.libstdc++
  msg2 'Removing unused library files'
  rm ${pkgdir}/opt/tmw/${pkgname}/sys/os/glnxa64/{libstdc++.so.6.0.17,libstdc++.so.6,libgcc_s.so.1,libgfortran.so.3.0.0,libgfortran.so.3,libquadmath.so.0.0.0,libquadmath.so.0}
}

## For network installations, apparently, a license file needs to be used for the installation. To perform a network install set _networkinstall to be true.

_networkinstall=false
# _networkinstall=true
if [ "${_networkinstall}" = true ]; then
  source+="${pkgname}.lic"
  md5sums+='SKIP'
fi

## The default installation behavior is to install all licensed products whether or not they are available in the iso file. To install only a subset of licensed products set _partialinstall to be true and remove unwanted entries from the _products array.

_partialinstall=false
# _partialinstall=true
_products=(
  "Aerospace_Blockset"
  "Aerospace_Toolbox"
  "Antenna_Toolbox"
  "Bioinformatics_Toolbox"
  "Communications_System_Toolbox"
  "Computer_Vision_System_Toolbox"
  "Control_System_Toolbox"
  "Curve_Fitting_Toolbox"
  "DO_Qualification_Kit"
  "DSP_System_Toolbox"
  "Data_Acquisition_Toolbox"
  "Database_Toolbox"
  "Datafeed_Toolbox"
  "Econometrics_Toolbox"
  "Embedded_Coder"
  "Filter_Design_HDL_Coder"
  "Financial_Instruments_Toolbox"
  "Financial_Toolbox"
  "Fixed_Point_Designer"
  "Fuzzy_Logic_Toolbox"
  "Gauges_Blockset"
  "Global_Optimization_Toolbox"
  "HDL_Coder"
  "HDL_Verifier"
  "IEC_Certification_Kit"
  "Image_Acquisition_Toolbox"
  "Image_Processing_Toolbox"
  "Instrument_Control_Toolbox"
  "LTE_System_Toolbox"
  "MATLAB"
  "MATLAB_Coder"
  "MATLAB_Compiler"
  "MATLAB_Compiler_SDK"
  "MATLAB_Distributed_Computing_Server"
  "MATLAB_Production_Server"
  "MATLAB_Report_Generator"
  "Mapping_Toolbox"
  "Model_Predictive_Control_Toolbox"
  "Model_Based_Calibration_Toolbox"
  "Neural_Network_Toolbox"
  "OPC_Toolbox"
  "Optimization_Toolbox"
  "Parallel_Computing_Toolbox"
  "Partial_Differential_Equation_Toolbox"
  "Phased_Array_System_Toolbox"
  "Polyspace_Bug_Finder"
  "Polyspace_Code_Prover"
  "RF_Toolbox"
  "Robotics_System_Toolbox"
  "Robust_Control_Toolbox"
  "Signal_Processing_Toolbox"
  "SimBiology"
  "SimDriveline"
  "SimElectronics"
  "SimEvents"
  "SimHydraulics"
  "SimMechanics"
  "SimPowerSystems"
  "SimRF"
  "Simscape"
  "Simulink"
  "Simulink_3D_Animation"
  "Simulink_Code_Inspector"
  "Simulink_Coder"
  "Simulink_Control_Design"
  "Simulink_Design_Optimization"
  "Simulink_Design_Verifier"
  "Simulink_Desktop_Real_Time"
  "Simulink_PLC_Coder"
  "Simulink_Real_Time"
  "Simulink_Report_Generator"
  "Simulink_Test"
  "Simulink_Verification_and_Validation"
  "Spreadsheet_Link_EX"
  "Stateflow"
  "Statistics_and_Machine_Learning_Toolbox"
  "Symbolic_Math_Toolbox"
  "System_Identification_Toolbox"
  "SystemTest"
  "Trading_Toolbox"
  "Vehicle_Network_Toolbox"
  "Vision_HDL_Toolbox"
  "Wavelet_Toolbox"
)
